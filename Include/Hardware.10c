; --------------------------------------------
; Title:   Hardware
; Author:  Batch-Man
; Date:    01/10/2012
; Version: 1.2.1
; --------------------------------------------
sub PC, 1 ; AntiExec Guard protects this area from contiguous access ;)

:HWInit
:detect_devs
hwn i               ; Retrieve number of devices
set j, 0            ; Initialize loop counter
ife i, 0
	set PC, POP

:init_devs
hwq j               
ife a, 0xf615					;LEM1802
  ife b, 0x7349
  	ife c, 0x1802
  		jsr init_lem1802
ife a, 0x7406
	ife b, 0x30cf
		ife c, 0x0001
			jsr init_keyb
ife a, 0xb402
	ife b, 0x12d0
		ife c, 0x0001
			jsr init_clock

add j, 1
ifn j, i
	set PC, init_devs
ias ISR
set PC, POP

:init_lem1802
set [screen_d], j

set a, 0         	  ; Device action, set VRAM address
set b, [screen_a]
hwi j

; Cls
set a, [screen_a]
set b, a
add b, 0x100
:cls_loop
set [a], 0x0000
add a, 1

ifn a, b
  set PC, cls_loop
set PC, POP

:init_keyb
set [keyb_d], j
set a, 3
set b, 0
hwi j			; Disabling keyboard, we have to define it's suitable behaviour before
set PC, POP

:init_clock
set [clock_d], j
set a, 0
set b, 1
hwi j
set a, 2
set b, j
hwi j
set PC, POP

:screen_d		; Screen dev ID
dat 0x0000
:screen_a		; VRAM pointer (/!\should be modified BEFORE HWInit call if needed but NEVER AFTER/!\)
dat 0x8000
:keyb_d			; Keyboard dev ID
dat 0x0000
:clock_d		; Clock dev ID
dat 0x0000